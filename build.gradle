buildscript {
  repositories {
    mavenLocal()
    maven { url "http://artifactory.reform.hmcts.net/artifactory/libs-release" }
    maven { url "http://repo1.maven.org/maven2/" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath group: 'org.sonarsource.scanner.gradle', name: 'sonarqube-gradle-plugin', version: '2.6.2'
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'io.spring.dependency-management' version '1.0.3.RELEASE'
  id 'org.springframework.boot' version '1.5.6.RELEASE'
  id 'org.owasp.dependencycheck' version '3.0.2'
  id 'com.github.ben-manes.versions' version '0.15.0'
}

apply plugin: 'org.sonarqube'
apply plugin: 'findbugs'
apply plugin: 'jacoco'

group = 'uk.gov.hmcts.probate'
version = "0.0.2"
mainClassName = 'uk.gov.hmcts.probate.BusinessRulesValidationApplication'

sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle.toolVersion = '8.2'
checkstyle.configFile = new File(rootDir, "checkstyle.xml")
mainClassName = 'uk.gov.hmcts.probate.BusinessRulesValidationApplication'


check.dependsOn dependencyCheckAnalyze
// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
  checkstyleTask.doLast {
    reports.all { report ->
      def outputFile = report.destination
      if (outputFile.exists() && outputFile.text.contains("<error ")) {
        throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
      }
    }
  }
}

jacoco {
  toolVersion = '0.8.0'
}

jacocoTestReport {
  group = "Reporting"
  reports {
    xml.enabled false
    html.destination "${buildDir}/reports/jacoco"
  }
}

tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

dependencyCheck {
  suppressionFiles = ['dependency-check-suppressions.xml']
}

findbugs {
  ignoreFailures = true
  reportLevel = "low"
}

sonarqube {
  properties {
    property "sonar.projectName", "PROBATE :: Sol-CCD-Service"
    property "sonar.projectKey", "Sols-CCD-Service-API"
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    property "sonar.host.url", "https://sonar.reform.hmcts.net/"
  }
}

test.finalizedBy jacocoTestReport

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'dependency-check-suppressions.xml'
}

sourceSets {
    smokeTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

def springBootVersion = '1.5.6.RELEASE'

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
  maven { url "http://artifactory.reform.hmcts.net/artifactory/libs-release" }
  maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  maven { url "http://repo1.maven.org/maven2/" }
  maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: springBootVersion

  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix', version: '1.3.4.RELEASE'
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix-dashboard', version: '1.3.4.RELEASE'

  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version: '1.2.1'
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: '1.2.1'
  compile group: 'uk.gov.hmcts.reform', name: 'http-proxy-spring-boot-autoconfigure', version: '1.1.0'
  compile group: 'uk.gov.hmcts.auth.checker', name: 'auth-checker-spring', version: '1.0.1'
  compile group: 'uk.gov.hmcts.auth.provider.service', name: 'service-token-generator-spring', version: '1.0.5'
  compile group: 'uk.gov.hmcts.auth.totp', name: 'totp', version: '1.0.1'

  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.2'
  compile group: 'commons-io', name: 'commons-io', version: '2.6'

  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.6.0'
  compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.6.0'
  compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: '0.23.0.RELEASE'
  compile group: 'org.projectlombok', name: 'lombok', version: '1.16.18'

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
  testCompile group: 'org.pdfbox', name: 'com.springsource.org.pdfbox', version: '0.7.3'
  testCompile group: 'org.springframework.security', name: 'spring-security-test', version: '4.2.5.RELEASE'
  testCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.7'

  smokeTestCompile sourceSets.main.runtimeClasspath
  smokeTestCompile sourceSets.test.runtimeClasspath
}

compileJava {
  options.compilerArgs << '-parameters'
}

jar {
  archiveName = 'sol-ccd-service.jar'
  manifest {

    attributes 'Implementation-Title': project.name,
            'Implementation-Version': project.version
  }
}

task printVersion {
  doLast {
    print project.version
  }
}

run {
  if (debug == 'true') {
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
  }
}