buildscript {
  ext {
    springBootVersion = '2.7.18'
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("net.serenity-bdd:serenity-gradle-plugin:2.5.8")
  }
}

plugins {
  id 'application'
  id 'io.spring.dependency-management' version '1.0.10.RELEASE'
  id 'org.springframework.boot' version '2.7.18'
  id 'uk.gov.hmcts.java' version '0.12.63'
  id 'com.github.ben-manes.versions' version '0.51.0'
  id 'com.gorylenko.gradle-git-properties' version '2.4.2'
  id 'info.solidsoft.pitest' version '1.6.0'
  id 'jacoco'
  id 'au.com.dius.pact' version '4.1.7' // do not change, otherwise serenity report fails
  id 'org.sonarqube' version '4.3.0.3225'
  id 'com.github.hmcts.rse-cft-lib' version '0.19.1277'
}

apply plugin: 'project-report'
apply plugin: 'jacoco'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: "com.gorylenko.gradle-git-properties"

group = 'uk.gov.hmcts.probate'
version = "4.0.0"
mainClassName = 'uk.gov.hmcts.probate.BusinessRulesValidationApplication'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

jacoco {
  toolVersion = '0.8.12'
}

sonarqube {
  properties {
    property "sonar.projectName", "PROBATE :: BACK-OFFICE"
    property "sonar.projectKey", "Sols-CCD-Service-API"
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    property "sonar.host.url", "https://sonar.reform.hmcts.net/"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.cpd.exclusions", "**/test/**," + "**/CallbackResponseTransformer.java," + "**/ResponseCaseData.java," + "**/CaseData.java," + "**/BlobUpload.java," +
            "**/BusinessRulesValidationApplication.java"
  }
}

test.finalizedBy jacocoTestReport

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'dependency-check-suppressions.xml'
}

pitest {
  targetClasses = ['uk.gov.hmcts.probate.*']
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 50
  timeoutConstInMillis = 30000
}

sonarqube {
  properties {
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
  }
}

sourceSets {
  testSmoke {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
  testFunctional {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }
  testCaseCreator {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/caseCreator/java')
    }
    resources.srcDir file('src/caseCreator/resources')
  }
  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}

test {
  maxHeapSize = "1024m"
  useJUnitPlatform()
}

processContractTestResources {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.testSmoke.output.classesDirs
  classpath = sourceSets.testSmoke.runtimeClasspath
}

task functional(type: Test) {
  // PACT_BRANCH_NAME is only set in non-dev environments
  if (System.getenv("PACT_BRANCH_NAME") == null) {
    doFirst() {
      "sh bin/variables/load-local-environment-variables.sh".execute()
    }
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').readLines().each() {
        def index = it.indexOf("=")
        def key = it.substring(0, index)
        def value = it.substring(index + 1)
        environment key, value
      }
    }
  }

  description = "Runs functional Tests"
  testClassesDirs = sourceSets.testFunctional.output.classesDirs
  classpath = sourceSets.testFunctional.runtimeClasspath
  finalizedBy aggregate
}

functional.finalizedBy(aggregate)


task caseCreator(type: Test) {
  description = "Runs functional Case creator Tests"
  testClassesDirs = sourceSets.testCaseCreator.output.classesDirs
  classpath = sourceSets.testCaseCreator.runtimeClasspath
  finalizedBy aggregate
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  useJUnitPlatform()
}

gitProperties {
  gitPropertiesDir = new File("${project.rootDir}/src/main/resources/uk/gov/hmcts/probate/sol")
  keys = ['git.commit.id','git.commit.time']
  dateFormat = "yyyy-MM-dd'T'HH:mmZ"
  dateFormatTimeZone = "GMT"
}

def versions = [
        springCloudStarterNetflixHystrix: '2.2.10.RELEASE',
        serenity                        : '2.6.0',
        serenityreporter                : '2.6.0',
        springBootVersion               : '2.7.18',
        springBootStarterHateoas        : '2.7.18',
        hmctsJavaLogging                : '6.1.5',
        authCheckerLib                  : '2.3.0',
        jacksonDatabind                 : '2.14.3',
        commonsIO                       : '2.16.1',
        commonsValidator                : '1.9.0',
        restAssured                     : '4.5.1',
        lombok                          : '1.18.32',
        springDocOpenUi                 : '1.7.0',
        springSecurityVersion           : '5.7.11',
        probateCommonsVersion           : '1.5.2',
        ccdClient                       : '4.9.2',
        mapStruct                       : '1.2.0.Final',
        serviceAuthVersion              : '4.1.2',
        pact_version                    : '4.1.34',
        probatePactCommonsVersion       : '1.0.2',
        springCloud                     : '3.1.8',
        elasticSearchVersion            : '7.15.0',
        documentClient                  : '1.7.3'
]

jacocoTestReport {
  executionData(test,integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://repo.maven.apache.org/maven2" }
  maven { url "https://jitpack.io" }
}


dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.9"
  }
  dependencies {
    dependency group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'
    // CVE-2022-24823
    dependencySet(group: 'io.netty', version: '4.1.111.Final') {
      entry 'netty-buffer'
      entry 'netty-codec'
      entry 'netty-codec-dns'
      entry 'netty-codec-http'
      entry 'netty-codec-http2'
      entry 'netty-codec-socks'
      entry 'netty-common'
      entry 'netty-handler'
      entry 'netty-handler-proxy'
      entry 'netty-resolver'
      entry 'netty-resolver-dns'
      entry 'netty-resolver-dns-classes-macos'
      entry 'netty-resolver-dns-native-macos'
      entry 'netty-tcnative-classes'
      entry 'netty-transport'
      entry 'netty-transport-classes-epoll'
      entry 'netty-transport-classes-kqueue'
      entry 'netty-transport-native-epoll'
      entry 'netty-transport-native-kqueue'
      entry 'netty-transport-native-unix-common'
    }
    // CVE-2022-24823
    dependencySet(group: 'io.netty', version: '2.0.65.Final') {
      entry 'netty-tcnative-classes'
    }
  }
}

configurations.all {
  resolutionStrategy {
    eachDependency { details ->
      if (details.requested.group in ['org.hibernate']) {
        details.useVersion '5.6.5.Final'
      }
      if (details.requested.group in ['org.apache.logging.log4j']) {
        details.useVersion '2.18.0'
      }
    }
  }
}

ext['snakeyaml.version'] = '2.2'

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBootVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web-services', version: versions.springBootVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBootVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBootVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: versions.springBootVersion
  implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.4.RELEASE'
  implementation group: 'com.github.hmcts', name: 'probate-commons', version: versions.probateCommonsVersion
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: versions.springCloud
  implementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: versions.documentClient
  implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '13.1'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: versions.springCloudStarterNetflixHystrix
  implementation ('io.github.openfeign.form:feign-form-spring') {
    version {
      strictly '3.8.0'
    }
  }

  implementation group: 'com.google.guava', name: 'guava', version: '33.2.1-jre'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: versions.springBootStarterHateoas
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.hmctsJavaLogging
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: versions.hmctsJavaLogging
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.serviceAuthVersion
  implementation group: 'com.github.hmcts', name: 'auth-checker-lib', version: versions.authCheckerLib
  implementation group: 'com.github.hmcts', name: 'cmc-pdf-service-client', version: '8.0.1'
  implementation group: 'com.github.hmcts', name: 'send-letter-client', version: '3.0.23'
  implementation group: 'com.github.hmcts', name: 'life-events-client', version: '1.0.7'
  implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.26.1'
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.4'

  implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jacksonDatabind
  implementation group: 'commons-io', name: 'commons-io', version: versions.commonsIO
  implementation group: 'commons-validator', name: 'commons-validator', version: versions.commonsValidator
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksonDatabind
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jacksonDatabind
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jacksonDatabind
  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: versions.springDocOpenUi
  implementation group: 'org.springdoc', name: 'springdoc-openapi-security', version: versions.springDocOpenUi
  implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  implementation group:'org.projectlombok', name: 'lombok', version: versions.lombok
  annotationProcessor group:'org.projectlombok', name:'lombok', version: versions.lombok
  implementation group: 'org.projectlombok', name: 'lombok-mapstruct-binding', version: '0.1.0'

  implementation group: 'org.elasticsearch.client', name: 'elasticsearch-rest-high-level-client', version: versions.elasticSearchVersion
  implementation group: 'org.elasticsearch.client', name: 'elasticsearch-rest-client', version: versions.elasticSearchVersion
  implementation group: 'org.elasticsearch', name: 'elasticsearch', version: versions.elasticSearchVersion
  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '6.3.0'

  implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: '10.7.4'
  implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
  implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'

  implementation group: 'org.pitest', name: 'pitest', version: '1.3.2'
  implementation group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.17.0'
  implementation 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0'
  implementation 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'

  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: versions.ccdClient
  implementation ('org.springframework.boot:spring-boot-starter-data-jpa')
  implementation ('org.postgresql:postgresql:42.4.4') {
    exclude module: 'slf4j-simple'
  }
  implementation group: 'org.mapstruct', name: 'mapstruct-jdk8', version: versions.mapStruct
  implementation group: 'org.mapstruct', name: 'mapstruct-processor', version: versions.mapStruct

  // Notifications client API
  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.0.1-RELEASE'
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.1.8'
  annotationProcessor group: 'org.mapstruct', name: 'mapstruct-processor', version: versions.mapStruct

  testAnnotationProcessor group:'org.projectlombok', name:'lombok', version: versions.lombok
  testImplementation group: 'com.github.hmcts', name: 'probate-commons', version: versions.probateCommonsVersion
  testImplementation("org.springframework.boot:spring-boot-starter-test:$versions.springBootVersion") {
    exclude group: 'com.vaadin.external.google', module:'android-json'
  }
  testImplementation group: 'com.h2database', name: 'h2', version: '1.4.197'
  testImplementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.30'
  testImplementation group: 'org.springframework.security', name: 'spring-security-test', version: versions.springSecurityVersion
  testImplementation group: 'com.mitchellbosecke', name: 'pebble', version: '2.4.0'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: versions.springBootStarterHateoas
  testImplementation group: 'com.github.hmcts', name: 'auth-checker-lib', version: versions.authCheckerLib
  testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.1'
  testImplementation 'org.awaitility:awaitility:4.1.0'
  testImplementation 'com.github.hmcts:fortify-client:1.3.0:all'
  testImplementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jacksonDatabind
  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '3.12.4'


  testSmokeImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
  testSmokeImplementation sourceSets.main.runtimeClasspath
  testSmokeImplementation sourceSets.test.runtimeClasspath

  testFunctionalImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  testFunctionalImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  testFunctionalImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  testFunctionalImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
  testFunctionalImplementation group: 'org.awaitility', name: 'awaitility', version: '3.1.6'
  testFunctionalImplementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.12.5'
  testFunctionalImplementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.serviceAuthVersion
  testFunctionalImplementation group: 'com.github.hmcts', name: 'cmc-pdf-service-client', version: '8.0.1'
  testFunctionalImplementation sourceSets.main.runtimeClasspath
  testFunctionalImplementation sourceSets.test.runtimeClasspath
  testFunctionalAnnotationProcessor group:'org.projectlombok', name: 'lombok', version: versions.lombok
  testFunctionalCompileOnly group:'org.projectlombok', name: 'lombok', version: versions.lombok
  testFunctionalImplementation ('io.github.openfeign.form:feign-form-spring') {
    version {
        strictly '3.8.0'
    }
  }

  testCaseCreatorImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
  testCaseCreatorImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
  testCaseCreatorImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
  testCaseCreatorImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
  testCaseCreatorImplementation group: 'net.serenity-bdd', name: 'serenity-single-page-report', version: versions.serenityreporter
  testCaseCreatorImplementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.12.5'
  testCaseCreatorImplementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: versions.serviceAuthVersion
  testCaseCreatorImplementation sourceSets.main.runtimeClasspath
  testCaseCreatorImplementation sourceSets.test.runtimeClasspath
  testCaseCreatorAnnotationProcessor group:'org.projectlombok', name: 'lombok', version: versions.lombok
  testCaseCreatorCompileOnly group:'org.projectlombok', name: 'lombok', version: versions.lombok

  contractTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksonDatabind
  contractTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jacksonDatabind
  contractTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jacksonDatabind
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'java8',  version: versions.pact_version
  contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
  contractTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.1")
  contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.7.1')
  contractTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.7.0'
  contractTestImplementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '6.3.0'

  contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  contractTestRuntimeOnly group: 'org.springframework.boot', name: 'spring-boot-starter-test'

  contractTestImplementation group: 'com.github.hmcts', name: 'probate-pact-commons', version: versions.probatePactCommonsVersion
  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath

  contractTestAnnotationProcessor group:'org.projectlombok', name: 'lombok', version: versions.lombok

  // Provides fast-reload of the probate service upon edit & compile during local development
  // cftlibImplementation 'org.springframework.boot:spring-boot-devtools'

  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-parameters'
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

bootJar {
  archiveFileName = 'back-office.jar'
  manifest {

    attributes 'Implementation-Title': project.name,
            'Implementation-Version': project.version
  }
}

task printVersion {
  doLast {
    print project.version
  }
}

run {
  if (debug == 'true') {
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
  }
}

project.ext {
  pacticipant = "probate_backOffice"
  pacticipantVersion = getCheckedOutGitCommitHash()
}

task runAndPublishConsumerPactTests(type: Test){
  logger.lifecycle("Runs consumer pact tests")
  systemProperty 'pact.rootDir', "pacts"
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath

}

task fortifyScan(type: JavaExec)  {
  mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

task highLevelDataSetup(type: JavaExec) {
  onlyIf {
    return false
  }
}

check.dependsOn(integration)
tasks.withType(Copy).all { duplicatesStrategy 'exclude' }

runAndPublishConsumerPactTests.finalizedBy pactPublish


static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactDirectory = 'pacts'
    tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
    version = project.pacticipantVersion
  }
}

// Build the back office excel file.
task buildBackOfficeXlsx(type: Exec) {
  environment 'CCD_DEF_CASE_SERVICE_BASE_URL', 'localhost:4104'

  def input = project.file('ccdImports/configFiles/CCD_Probate_Backoffice')
  commandLine project.file("ccdImports/conversionScripts/convertJsonToXLS.sh")
  args input
  // Register the inputs and outputs so Gradle can skip if up-to-date.
  inputs.dir input
  outputs.file project.file('jsonToXLS/CCD_Probate_Backoffice.xlsx')
}

// Build the all excel files.
task buildAllXlsx(type: Exec) {
  def input = 'localhost:4104'
  def input2 = 'aac-manage-case-assignment-aat.service.core-compute-aat.internal'
  commandLine project.file("ccdImports/conversionScripts/createAllXLS.sh")
  args input, input2
  // Register the inputs and outputs so Gradle can skip if up-to-date.
  outputs.dir project.file('jsonToXLS')
}

// Force Build the all excel files.
task forceBuildAllXlsx(type: Exec) {
  def input = 'localhost:4104'
  def input2 = 'aac-manage-case-assignment-aat.service.core-compute-aat.internal'
  commandLine project.file("ccdImports/conversionScripts/createAllXLS.sh")
  args input, input2
}

//import all excel files.
task importAllXlsx(type: Exec) {
  doFirst() {
    "sh bin/variables/load-local-environment-variables.sh".execute()
  }

  if (project.file('./.aat-env').exists()) {
    project.file('./.aat-env').readLines().each() {
      def index = it.indexOf("=")
      def key = it.substring(0, index)
      def value = it.substring(index + 1)
      environment key, value
    }
  }

  commandLine project.file("ccdImports/conversionScripts/importAllXLS.sh")
}

task buildAndImportAllXlsx(type: Exec) {
  dependsOn forceBuildAllXlsx

  doFirst() {
    "sh bin/variables/load-local-environment-variables.sh".execute()
  }

  if (project.file('./.aat-env').exists()) {
    project.file('./.aat-env').readLines().each() {
      def index = it.indexOf("=")
      def key = it.substring(0, index)
      def value = it.substring(index + 1)
      environment key, value
    }
  }

  commandLine project.file("ccdImports/conversionScripts/importAllXLS.sh")
}

bootWithCCD {
  // Request the cftlib create the probate_man database
  environment 'RSE_LIB_ADDITIONAL_DATABASES', 'probate_man'
  environment 'PROBATE_POSTGRESQL_PORT', '6432'
  environment 'PROBATE_POSTGRESQL_USER', 'postgres'
  environment 'PROBATE_POSTGRESQL_PASSWORD', 'postgres'

  if (new Boolean(System.getenv('USE_LOCAL_SUPPORT_SERVICES'))) {
    dependsOn buildBackOfficeXlsx
    authMode = uk.gov.hmcts.rse.AuthMode.Local
    environment 'IDAM_S2S_URL', 'http://localhost:8489'
    environment 'IDAM_SERVICE_HOST', 'http://localhost:5000'
  } else {
    doFirst() {
      "sh bin/variables/load-local-environment-variables.sh".execute()
    }

    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').readLines().each() {
        def index = it.indexOf("=")
        def key = it.substring(0, index)
        def value = it.substring(index + 1)
        environment key, value
      }
    }

    dependsOn buildAllXlsx
  }
}

task reloadLocalEnvVars() {
  doFirst() {
    "sh bin/variables/load-local-environment-variables.sh".execute()
  }
}

bootJar {
  getArchiveFileName().set(provider {
    'back-office.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }

dependencyCheck {
    analyzers {
      nodeAudit {
        yarnEnabled = false
      }
    }
  }
}

rootProject.tasks.named("processResources") {
  dependsOn("generateGitProperties")
}
