buildscript {
  ext {
    springBootVersion = '2.0.6.RELEASE'
  }
  repositories {
    jcenter()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath group: 'org.sonarsource.scanner.gradle', name: 'sonarqube-gradle-plugin', version: '2.6.2'
    classpath("net.serenity-bdd:serenity-gradle-plugin:2.0.11")
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'io.spring.dependency-management' version '1.0.6.RELEASE'
  id 'org.springframework.boot' version '2.0.6.RELEASE'
  id 'org.owasp.dependencycheck' version '3.2.1'
  id 'com.github.ben-manes.versions' version '0.15.0'
  id 'com.gorylenko.gradle-git-properties' version '1.4.21'
  id 'info.solidsoft.pitest' version '1.3.0'
}

apply plugin: 'org.sonarqube'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: "com.gorylenko.gradle-git-properties"

group = 'uk.gov.hmcts.probate'
version = "4.0.0"
mainClassName = 'uk.gov.hmcts.probate.BusinessRulesValidationApplication'

sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle.toolVersion = '8.2'
checkstyle.configFile = new File(rootDir, "checkstyle.xml")
mainClassName = 'uk.gov.hmcts.probate.BusinessRulesValidationApplication'

ext['selenium.version']='3.8.1'

check.dependsOn dependencyCheckAnalyze
// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
  checkstyleTask.doLast {
    reports.all { report ->
      def outputFile = report.destination
      if (outputFile.exists() && outputFile.text.contains("<error ")) {
        throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
      }
    }
  }
}

jacoco {
  toolVersion = '0.8.0'
}

jacocoTestReport {
  group = "Reporting"
  reports {
    xml.enabled false
    html.destination "${buildDir}/reports/jacoco"
  }
}

tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

findbugs {
  ignoreFailures = true
  reportLevel = "low"
}

sonarqube {
  properties {
    property "sonar.projectName", "PROBATE :: BACK-OFFICE"
    property "sonar.projectKey", "Sols-CCD-Service-API"
    property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    property "sonar.host.url", "https://sonar.reform.hmcts.net/"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
  }
}

test.finalizedBy jacocoTestReport

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'dependency-check-suppressions.xml'
}

pitest {
  targetClasses = ['uk.gov.hmcts.probate.*']
  threads = 10
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 50
  timeoutConstInMillis = 30000
}

sonarqube {
  properties {
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
  }
}

sourceSets {
  testSmoke {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
  testFunctional {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }
  testCaseCreator {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/caseCreator/java')
    }
    resources.srcDir file('src/caseCreator/resources')
  }
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.testSmoke.output.classesDirs
  classpath = sourceSets.testSmoke.runtimeClasspath
}

task functional(type: Test) {
  description = "Runs functional Tests"
  testClassesDirs = sourceSets.testFunctional.output.classesDirs
  classpath = sourceSets.testFunctional.runtimeClasspath
  finalizedBy aggregate
}

task caseCreator(type: Test) {
  description = "Runs functional Case creator Tests"
  testClassesDirs = sourceSets.testCaseCreator.output.classesDirs
  classpath = sourceSets.testCaseCreator.runtimeClasspath
  finalizedBy aggregate
}

gitProperties {
  gitPropertiesDir = new File("${project.rootDir}/src/main/resources/uk/gov/hmcts/probate/sol")
  keys = ['git.commit.id','git.commit.time']
  dateFormat = "yyyy-MM-dd'T'HH:mmZ"
  dateFormatTimeZone = "GMT"
}

def versions = [
        springCloudStarterNetflixHystrix: '2.0.2.RELEASE',
        serenitySpring: '1.0.26',
        applicationinsights: '2.1.2',
        springBootVersion: '2.0.6.RELEASE',
        reformsJavaLoggingVersion: '3.0.2',
        springSecurityVersion : '5.1.1.RELEASE'
]

jacocoTestReport {
  executionData(test)
  reports {
    xml.enabled = true
    csv.enabled = false
    xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
  maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
  maven { url "http://repo.maven.apache.org/maven2" }
}


processResources {
  from ('src/lib/AI-Agent.xml') {
    into 'lib'
  }
}

dependencies {
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web-services', version: versions.springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: versions.springBootVersion
  compile group: 'org.springframework.retry', name: 'spring-retry', version: '1.2.2.RELEASE'

  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: versions.springCloudStarterNetflixHystrix
  compile ('io.github.openfeign.form:feign-form-spring:3.4.1') {
    force = true
  }

  compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: '0.23.0.RELEASE'

  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version: versions.reformsJavaLoggingVersion
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: versions.reformsJavaLoggingVersion
  compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: versions.reformsJavaLoggingVersion
  compile group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: '2.1.3'
  compile group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '3.9.2-RELEASE'
  compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '1.0.4'
  compile group: 'uk.gov.hmcts.reform', name: 'pdf-service-client', version: '6.0.0'
  compile group: 'uk.gov.hmcts.reform', name: 'send-letter-client', version: '2.1.2'

  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.9.2'
  compile group: 'commons-io', name: 'commons-io', version: '2.6'

  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.6.0'
  compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.6.0'
  compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: '0.23.0.RELEASE'
  compile group: 'org.projectlombok', name: 'lombok', version: '1.16.18'

  compile group: 'org.pitest', name: 'pitest', version: '1.3.2'
  compile group: 'com.atlassian.commonmark', name: 'commonmark', version: '0.11.0'
  compile 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0'
  compile 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'

  testCompile("org.springframework.boot:spring-boot-starter-test:$versions.springBootVersion") {
    exclude group: 'com.vaadin.external.google', module:'android-json'
  }
  testCompile group: 'org.pdfbox', name: 'com.springsource.org.pdfbox', version: '0.7.3'
  testCompile group: 'org.springframework.security', name: 'spring-security-test', version: versions.springSecurityVersion
  testCompile group: 'com.mitchellbosecke', name: 'pebble', version: '2.4.0'

  testSmokeCompile group: 'io.rest-assured', name: 'rest-assured', version: '3.0.7'
  testSmokeCompile sourceSets.main.runtimeClasspath
  testSmokeCompile sourceSets.test.runtimeClasspath

  testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-core', version: '2.0.11'
  testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: '2.0.11'
  testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: '2.0.11'
  testFunctionalCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: '2.0.11'
  testFunctionalCompile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'
  testFunctionalCompile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '1.0.4'
  testFunctionalCompile group: 'uk.gov.hmcts.reform', name: 'pdf-service-client', version: '6.0.0'
  testFunctionalCompile sourceSets.main.runtimeClasspath
  testFunctionalCompile sourceSets.test.runtimeClasspath
  testFunctionalCompile ('io.github.openfeign.form:feign-form-spring:3.4.1') {
    force = true
  }

  testCaseCreatorCompile group: 'net.serenity-bdd', name: 'serenity-core', version: '2.0.11'
  testCaseCreatorCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: '2.0.11'
  testCaseCreatorCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: '2.0.11'
  testCaseCreatorCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: '2.0.11'
  testCaseCreatorCompile group: 'io.jsonwebtoken', name: 'jjwt', version: '0.9.0'
  testCaseCreatorCompile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '1.0.4'
  testCaseCreatorCompile sourceSets.main.runtimeClasspath
  testCaseCreatorCompile sourceSets.test.runtimeClasspath
}

compileJava {
  options.compilerArgs << '-parameters'
}

bootJar {
  archiveName = 'back-office.jar'
  manifest {

    attributes 'Implementation-Title': project.name,
            'Implementation-Version': project.version
  }
}

task printVersion {
  doLast {
    print project.version
  }
}

run {
  if (debug == 'true') {
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
  }
}